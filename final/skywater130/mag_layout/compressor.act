import "dataflow.act";
import "channel.act";




// array of MUX2X1, now that i think about it, could be done better
defproc choose(bd?<32> X,Y,Z; bd!<32> R) 
{
  copy<32> x_copy(X);

  // x & y
  and<32> XY(x_copy.R1, Y);

  // ~x & z
  inv<32> x_not(x_copy.R2);
  and<32> nXZ(x_not.Z, Z);

  xor<32> xor(XY.Z, nXZ.Z, R);
}

// similar, array of maj?
defproc maj(bd?<32> X,Y,Z; bd!<32> R) 
{
  copy<32> x_copy(X);
  copy<32> y_copy(Y);
  copy<32> z_copy(Z);

  and<32> XY(x_copy.R1, y_copy.R1);
  and<32> XZ(x_copy.R2, z_copy.R1);
  and<32> YZ(y_copy.R2, z_copy.R2);

  xor<32> xor1(XY.Z, XZ.Z);
  xor<32> xor2(xor1.Z, YZ.Z, R);
}

defproc big_sigma0(bd?<32> L; bd!<32> R){
  // will check with alex about copying
  copy<32> in_copy(L);

  rot<32,2>  rot1(in_copy.R1);
  rot<32,13> rot2(in_copy.R1);
  rot<32,22> rot3(in_copy.R2);

  xor<32> xor1(rot1.R,rot2.R);
  xor<32> xor2(rot3.R,xor1.Z,R);
}

defproc big_sigma1(bd?<32> L; bd!<32> R){
  copy<32> in_copy(L);

  rot<32,6>  rot1(in_copy.R1);
  rot<32,11> rot2(in_copy.R1);
  rot<32,25> rot3(in_copy.R2);

  xor<32> xor1(rot1.R,rot2.R);
  xor<32> xor2(rot3.R,xor1.Z,R);
}

defproc generate_alpha(bd?<32> A,B,C,t1; bd!<32> R)
{
  copy<32> A_copy(A);

  big_sigma0 bs(A_copy.R1);
  maj ma(A_copy.R2, B, C);

  add<32> add1(t1, ma.R);
  add<32> add2(add1.Z, bs.R, R);
}

defproc generate_t1(bd?<32> w,K,E,F,G,H; bd!<32> R)
{
  copy<32> E_copy(E);

  big_sigma1 bs(E_copy.R1);
  choose ch(E_copy.R2, F, G);


  add<32> add2(ch.R, K);
  add<32> add3(H, w);

  add<32> add4(bs.R, add2.Z);

  add<32> add5(add3.Z,add4.Z,R);
}


defproc k_source(bd!<32> R)
{
  init_buffer<32, 1116352408>   k_00;
  init_buffer<32, 1899447441>   k_01;
  init_buffer<32, 3049323471>   k_02;
  init_buffer<32, 3921009573>   k_03;
  init_buffer<32, 961987163>    k_04;
  init_buffer<32, 1508970993>   k_05;
  init_buffer<32, 2453635748>   k_06;
  init_buffer<32, 2870763221>   k_07;
  init_buffer<32, 3624381080>   k_08;
  init_buffer<32, 310598401>    k_09;
  init_buffer<32, 607225278>    k_10;
  init_buffer<32, 1426881987>   k_11;
  init_buffer<32, 1925078388>   k_12;
  init_buffer<32, 2162078206>   k_13;
  init_buffer<32, 2614888103>   k_14;
  init_buffer<32, 3248222580>   k_15;
  init_buffer<32, 3835390401>   k_16;
  init_buffer<32, 4022224774>   k_17;
  init_buffer<32, 264347078>    k_18;
  init_buffer<32, 604807628>    k_19;
  init_buffer<32, 770255983>    k_20;
  init_buffer<32, 1249150122>   k_21;
  init_buffer<32, 1555081692>   k_22;
  init_buffer<32, 1996064986>   k_23;
  init_buffer<32, 2554220882>   k_24;
  init_buffer<32, 2821834349>   k_25;
  init_buffer<32, 2952996808>   k_26;
  init_buffer<32, 3210313671>   k_27;
  init_buffer<32, 3336571891>   k_28;
  init_buffer<32, 3584528711>   k_29;
  init_buffer<32, 113926993>    k_30;
  init_buffer<32, 338241895>    k_31;
  init_buffer<32, 666307205>    k_32;
  init_buffer<32, 773529912>    k_33;
  init_buffer<32, 1294757372>   k_34;
  init_buffer<32, 1396182291>   k_35;
  init_buffer<32, 1695183700>   k_36;
  init_buffer<32, 1986661051>   k_37;
  init_buffer<32, 2177026350>   k_38;
  init_buffer<32, 2456956037>   k_39;
  init_buffer<32, 2730485921>   k_40;
  init_buffer<32, 2820302411>   k_41;
  init_buffer<32, 3259730800>   k_42;
  init_buffer<32, 3345764771>   k_43;
  init_buffer<32, 3516065817>   k_44;
  init_buffer<32, 3600352804>   k_45;
  init_buffer<32, 4094571909>   k_46;
  init_buffer<32, 275423344>    k_47;
  init_buffer<32, 430227734>    k_48;
  init_buffer<32, 506948616>    k_49;
  init_buffer<32, 659060556>    k_50;
  init_buffer<32, 883997877>    k_51;
  init_buffer<32, 958139571>    k_52;
  init_buffer<32, 1322822218>   k_53;
  init_buffer<32, 1537002063>   k_54;
  init_buffer<32, 1747873779>   k_55;
  init_buffer<32, 1955562222>   k_56;
  init_buffer<32, 2024104815>   k_57;
  init_buffer<32, 2227730452>   k_58;
  init_buffer<32, 2361852424>   k_59;
  init_buffer<32, 2428436474>   k_60;
  init_buffer<32, 2756734187>   k_61;
  init_buffer<32, 3204031479>   k_62;
  init_buffer<32, 3329325298>   k_63;

  k_62.L = k_63.R;
  k_61.L = k_62.R;
  k_60.L = k_61.R;
  k_59.L = k_60.R;
  k_58.L = k_59.R;
  k_57.L = k_58.R;
  k_56.L = k_57.R;
  k_55.L = k_56.R;
  k_54.L = k_55.R;
  k_53.L = k_54.R;
  k_52.L = k_53.R;
  k_51.L = k_52.R;
  k_50.L = k_51.R;
  k_49.L = k_50.R;
  k_48.L = k_49.R;
  k_47.L = k_48.R;
  k_46.L = k_47.R;
  k_45.L = k_46.R;
  k_44.L = k_45.R;
  k_43.L = k_44.R;
  k_42.L = k_43.R;
  k_41.L = k_42.R;
  k_40.L = k_41.R;
  k_39.L = k_40.R;
  k_38.L = k_39.R;
  k_37.L = k_38.R;
  k_36.L = k_37.R;
  k_35.L = k_36.R;
  k_34.L = k_35.R;
  k_33.L = k_34.R;
  k_32.L = k_33.R;
  k_31.L = k_32.R;
  k_30.L = k_31.R;
  k_29.L = k_30.R;
  k_28.L = k_29.R;
  k_27.L = k_28.R;
  k_26.L = k_27.R;
  k_25.L = k_26.R;
  k_24.L = k_25.R;
  k_23.L = k_24.R;
  k_22.L = k_23.R;
  k_21.L = k_22.R;
  k_20.L = k_21.R;
  k_19.L = k_20.R;
  k_18.L = k_19.R;
  k_17.L = k_18.R;
  k_16.L = k_17.R;
  k_15.L = k_16.R;
  k_14.L = k_15.R;
  k_13.L = k_14.R;
  k_12.L = k_13.R;
  k_11.L = k_12.R;
  k_10.L = k_11.R;
  k_09.L = k_10.R;
  k_08.L = k_09.R;
  k_07.L = k_08.R;
  k_06.L = k_07.R;
  k_05.L = k_06.R;
  k_04.L = k_05.R;
  k_03.L = k_04.R;
  k_02.L = k_03.R;
  k_01.L = k_02.R;
  k_00.L = k_01.R;

  slack<32, 32> loop_slack;
  copy<32> out(k_00.R, loop_slack.L, R);
  k_63.L = loop_slack.R;
}

template <pint init_hash>
defproc init_hash_control(bd?<32> in; bd?<1> control_final_out,control_init; bd!<32> forward, final_out){

  bd<32> forward_next;
  // Get a value in a decide whether it exits compressors or continues onwards
  split<32> value_in(in, control_final_out, forward_next, final_out);

  df_source<32,init_hash> src;
  // Takes in an initial value or the previous value
  merge<32> value_out(src.R, forward_next, control_init, forward);

}

template <pint init_hash>
defproc final_add(bd?<32> L; bd!<32> R){
  df_source<32,init_hash> src;
  add<32> a(L,src.R,R);
}

template <pint N>
defproc copy8(bd?<N> in; bd!<N> a,b,c,d,e,f,g,h){
  copy<N> c1(in);

  copy<N> c21(c1.R1);
  copy<N> c22(c1.R2);

  copy<N> c31(c21.R1,a,b);
  copy<N> c32(c21.R2,c,d);
  copy<N> c33(c22.R1,e,f);
  copy<N> c34(c22.R2,g,h);
}

defproc controller_compressor(bd!<1> final_out_control,forward_source_control){
  //final_out
  init_buf_slack<1,63,0> continue_next;
  init_buf_slack<1,1,1> final_output;
  slack<1,32> final_out_slack;

  final_out_slack.R = final_output.L;
  final_output.R = continue_next.L;
  copy<1> final(continue_next.R, final_out_control, final_out_slack.L);

  //forward_source_control
  init_buf_slack<1,1,1> init_value;
  init_buf_slack<1,63,0> pass_forward;
  slack<1,32> forward_source_slack;

  forward_source_slack.R = pass_forward.L;
  pass_forward.R = init_value.L;
  copy<1> forward(init_value.R, forward_source_control, forward_source_slack.L);

}

defproc compressor(bd?<32> word_in; bd!<32> a,b,c,d,e,f,g,h){
  //Controller Signals
  controller_compressor control;
  copy8<1> c_final_out_control(control.final_out_control);
  copy8<1> c_forward_source_control(control.forward_source_control);

  //Signals
  bd<32> calculated_a, calculated_b, calculated_c,calculated_d,calculated_e,calculated_f,calculated_g,calculated_h;
  bd<32> next_a, next_b, next_c,next_d,next_e,next_f,next_g,next_h;

  // Initializers
  init_hash_control<0b01101010000010011110011001100111> a_init(calculated_a,c_final_out_control.a,c_forward_source_control.a,next_a);
  init_hash_control<0b10111011011001111010111010000101> b_init(calculated_b,c_final_out_control.b,c_forward_source_control.b,next_b);
  init_hash_control<0b00111100011011101111001101110010> c_init(calculated_c,c_final_out_control.c,c_forward_source_control.c,next_c);
  init_hash_control<0b10100101010011111111010100111010> d_init(calculated_d,c_final_out_control.d,c_forward_source_control.d,next_d);
  init_hash_control<0b01010001000011100101001001111111> e_init(calculated_e,c_final_out_control.e,c_forward_source_control.e,next_e);
  init_hash_control<0b10011011000001010110100010001100> f_init(calculated_f,c_final_out_control.f,c_forward_source_control.f,next_f);
  init_hash_control<0b00011111100000111101100110101011> g_init(calculated_g,c_final_out_control.g,c_forward_source_control.g,next_g);
  init_hash_control<0b01011011111000001100110100011001> h_init(calculated_h,c_final_out_control.h,c_forward_source_control.h,next_h);

  //TEMP 1 / T1 calculation
  //defproc generate_epsilon(bd?<32> w,K,E,F,G,H; bd!<32> R)
  k_source k_values;
  copy<32> c_next_e(next_e);
  copy<32> c_next_f(next_f);
  copy<32> c_next_g(next_g);
  generate_t1 t1(word_in,k_values.R,c_next_e.R1,c_next_f.R1,c_next_g.R1,next_h);

  // new Alpha Calculation or a 
  // defproc generate_alpha(bd?<32> A,B,C,t1; bd!<32> R)
  copy<32> c_next_a(next_a);
  copy<32> c_next_b(next_b);
  copy<32> c_next_c(next_c);
  copy<32> c_t1(t1.R);
  generate_alpha alpha(c_next_a.R1,c_next_b.R1,c_next_c.R1,c_t1.R1);


  // Forwarding Step
  calculated_a = alpha.R;
  calculated_b = c_next_a.R2;
  calculated_c = c_next_b.R2;
  calculated_d = c_next_c.R2;

  add<32> epsilon(c_t1.R2,next_d);
  calculated_e = epsilon.Z;
  calculated_f = c_next_e.R2;
  calculated_g = c_next_f.R2;
  calculated_h = c_next_g.R2;

  // Final Output
  final_add<0b01101010000010011110011001100111> final_a(a_init.final_out,a);
  final_add<0b10111011011001111010111010000101> final_b(b_init.final_out,b);
  final_add<0b00111100011011101111001101110010> final_c(c_init.final_out,c);
  final_add<0b10100101010011111111010100111010> final_d(d_init.final_out,d);


  final_add<0b01010001000011100101001001111111> final_e(e_init.final_out,e);
  final_add<0b10011011000001010110100010001100> final_f(f_init.final_out,f);
  final_add<0b00011111100000111101100110101011> final_g(g_init.final_out,g);
  final_add<0b01011011111000001100110100011001> final_h(h_init.final_out,h);

}








